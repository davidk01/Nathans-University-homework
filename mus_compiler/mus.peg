start = sequence_block

repeat = "|: " s:sequence_block " :|" c:number? 
  {return {tag:'repeat', section:s, count:(c || 2)};}

sequence_block = l:parallel_sequence [\r\n] [\r\n] r:sequence_block 
  {return {tag:'seq', left:l, right:r};} / parallel_sequence

parallel_sequence = l:note_sequence [\r\n] r:parallel_sequence 
  {return {tag:'par', left:l, right:r};} / note_sequence

note_sequence = h:(note / repeat) " " t:note_sequence
  {return {tag:'seq',left:h,right:t};} / note / repeat

note = n:[cdefgab] a:accidental? o:octave_shift d:duration 
  {return {tag:'note', pitch:(n + a + o), dur:d};} / rest

rest = "_" d:duration {return {tag:'rest', dur:d};}

octave_shift = [0-8]

accidental = [#b]

number = a:[1-9] b:[0-9]* {return parseInt(a + b.join(""));}

duration = "[" d:number "]" {return d;}